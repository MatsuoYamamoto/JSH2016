**********************************************************************;
* Project           : JSH201609
*
* Program name      : JSH201609_ADS_ADS.sas
*
* Author            : MATSUO YAMAMOTO
*
* Date created      : 20160908
*
* Purpose           : Create Analysis DataSet
*
* Revision History  : 
*
* Date        Author           Ref    Revision (Date in YYYYMMDD format)
* 
*
**********************************************************************;

/*** Initial setting ***/
%MACRO CURRENT_DIR;

    %LOCAL _FULLPATH _PATH;
    %LET   _FULLPATH = ;
    %LET   _PATH     = ;

    %IF %LENGTH(%SYSFUNC(GETOPTION(SYSIN))) = 0 %THEN
        %LET _FULLPATH = %SYSGET(SAS_EXECFILEPATH);
    %ELSE
        %LET _FULLPATH = %SYSFUNC(GETOPTION(SYSIN));

    %LET _PATH = %SUBSTR(   &_FULLPATH., 1, %LENGTH(&_FULLPATH.)
                          - %LENGTH(%SCAN(&_FULLPATH.,-1,'\')) -1 );

    &_PATH.

%MEND CURRENT_DIR;

%LET _PATH2 = %CURRENT_DIR;
%LET FILE = ADS;

%INCLUDE "&_PATH2.\JSH201609_ADS_LIBNAME.sas";

/*** CSV read ***/
PROC IMPORT OUT= MAIN
  DATAFILE="&RAW.\JSH_report_160905_1920_STD.csv"
  DBMS=CSV REPLACE;
  GETNAMES=YES;
  DATAROW=2;
  GUESSINGROWS=2000; 
RUN; 

PROC IMPORT OUT= EXT
  DATAFILE="&EXT.\MHCOD.csv"
  DBMS=CSV REPLACE;
  GETNAMES=YES;
  DATAROW=2;
  GUESSINGROWS=2000; 
RUN; 

/*** AGE AND DROP ***/
DATA  WK01;
  DROP DTHFL DTHDTC DSSTDTC;
  SET  MAIN;
  IF  BRTHDTC=. OR MHSTDTC=. THEN DELETE;
  IF  MHDECOD >= 1000 THEN DELETE;
  AGEN = YRDIF(BRTHDTC,MHSTDTC);
  IF  AGEN>=0 THEN AGEN=INT(AGEN);
  ELSE AGEN=INT(AGEN-1);
  IF  AGEN<=0 THEN AGEN=AGEN+100;
  AREA=INT(SITEID/10000000);
  IF  0 <= AGEN < 5 THEN AGECAT1N=19;
  ELSE IF  5  <= AGEN < 10 THEN AGECAT1N=18;
  ELSE IF  10 <= AGEN < 15 THEN AGECAT1N=17;
  ELSE IF  15 <= AGEN < 20 THEN AGECAT1N=16;
  ELSE IF  20 <= AGEN < 25 THEN AGECAT1N=15;
  ELSE IF  25 <= AGEN < 30 THEN AGECAT1N=14;
  ELSE IF  30 <= AGEN < 35 THEN AGECAT1N=13;
  ELSE IF  35 <= AGEN < 40 THEN AGECAT1N=12;
  ELSE IF  40 <= AGEN < 45 THEN AGECAT1N=11;
  ELSE IF  45 <= AGEN < 50 THEN AGECAT1N=10;
  ELSE IF  50 <= AGEN < 55 THEN AGECAT1N=9;
  ELSE IF  55 <= AGEN < 60 THEN AGECAT1N=8;
  ELSE IF  60 <= AGEN < 65 THEN AGECAT1N=7;
  ELSE IF  65 <= AGEN < 70 THEN AGECAT1N=6;
  ELSE IF  70 <= AGEN < 75 THEN AGECAT1N=5;
  ELSE IF  75 <= AGEN < 80 THEN AGECAT1N=4;
  ELSE IF  80 <= AGEN < 85 THEN AGECAT1N=3;
  ELSE IF  85 <= AGEN < 90 THEN AGECAT1N=2;
  ELSE IF  90 <= AGEN      THEN AGECAT1N=1;
  ELSE  AGECAT1N=99;

  IF  0 <= AGEN < 16 THEN AGECAT2N=1;
  ELSE IF  16  <= AGEN < 30 THEN AGECAT2N=2;
  ELSE IF  30 <= AGEN < 65 THEN AGECAT2N=3;
  ELSE IF  65 <= AGEN      THEN AGECAT2N=4;
  ELSE  AGECAT2N=99;

RUN ;

/*** MH ***/
PROC SORT DATA=WK01 ; BY MHDECOD; RUN ;
PROC SORT DATA=EXT ; BY MHDECOD; RUN ;

DATA  WK02;
  MERGE  WK01(IN=A) EXT;
  BY  MHDECOD;
  IF A;
RUN ;

DATA  LIBADS.&FILE.;
  SET  WK02;
RUN;

%ADS_FIN;

/*** END ***/

**********************************************************************;
* Project           : JSH201609
*
* Program name      : JSH201609_ADS_ADS.sas
*
* Author            : MATSUO YAMAMOTO
*
* Date created      : 20160908
*
* Purpose           : Create Analysis DataSet
*
* Revision History  : 
*
* Date        Author           Ref    Revision (Date in YYYYMMDD format)
* 
*
**********************************************************************;
/*** Initial setting ***/
%MACRO FILE_SEPARATOR;
  %LOCAL _separator;
  %LET   _separator = ;
  %IF &sysscp=LIN X64 
    %THEN %LET _separator=/;
    %ELSE %LET _separator=\;
  &_separator.
%MEND FILE_SEPARATOR;
%LET _FS = %FILE_SEPARATOR;
%MACRO CURRENT_DIR;
  %LOCAL _FULLPATH _PATH;
  %LET   _FULLPATH = ;
  %LET   _PATH     = ;
  %IF &sysscp=WIN %THEN %DO;
    %IF %LENGTH(%SYSFUNC(GETOPTION(SYSIN))) = 0 %THEN
        %LET _FULLPATH = %SYSGET(SAS_EXECFILEPATH);
    %ELSE
        %LET _FULLPATH = %SYSFUNC(GETOPTION(SYSIN));
  %END;
  %IF &sysscp=LIN X64 %THEN %DO;
    %LET _FULLPATH = &_SASPROGRAMFILE;
  %END;
  %LET _PATH = %SUBSTR(   &_FULLPATH., 1, %LENGTH(&_FULLPATH.)
                          - %LENGTH(%SCAN(&_FULLPATH.,-1,&_FS.)) -1 );
  &_PATH.
%MEND CURRENT_DIR;

%LET _PATH2 = %CURRENT_DIR;
%LET FILE = ADS;

%INCLUDE "&_PATH2.&_FS.JSH201609_ADS_LIBNAME.sas";

/*** CSV read ***/
FILENAME IN "&EXT.&_FS.disease_161021.csv" Encoding="utf-8";
PROC IMPORT OUT= EXT
  DATAFILE=IN
  DBMS=CSV REPLACE;
  GETNAMES=NO;
  DATAROW=1;
  GUESSINGROWS=2000; 
RUN;
PROC DATASETS;
  MODIFY EXT;
  RENAME VAR1=category VAR2=code VAR3=abbr VAR4=name_ja VAR5=name_en VAR6=order VAR7=group_code VAR8=group_abbr VAR9=group_name_ja VAR10=group_name_en VAR11=group_type VAR12=group_order;
  RENAME code=MHDECOD group_code=MHGRPCOD name_ja=MHTERM_J group_name_ja=MHGRPTERM_J abbr=MHTERM group_abbr=MHGRPTERM order=MHREFID;
QUIT;
DATA  EXT;
  SET EXT;
  IF  category="epilepsy" THEN DELETE;
RUN;

/*** CSV read ***/
FILENAME IN "&RAW.&_FS.dataset_JSH_report2016.csv" Encoding="sjis";
PROC IMPORT OUT= MAIN
  DATAFILE=IN
  DBMS=CSV REPLACE;
  GETNAMES=YES;
  DATAROW=2;
  GUESSINGROWS=2000; 
RUN; 

/*** AGE AND DROP ***/
DATA  WK01;
  DROP DTHFL DTHDTC DSSTDTC;
  SET  MAIN;
  IF  INPUT(BRTHDTC,?? YYMMDD10.)=. OR INPUT(MHSTDTC,?? YYMMDD10.)=. THEN DELETE;
  IF  MHDECOD >= 1000 THEN DELETE;
  AGEN = YRDIF(INPUT(BRTHDTC,YYMMDD10.),INPUT(MHSTDTC,YYMMDD10.));
  IF  AGEN>=0 THEN AGEN=INT(AGEN);
  ELSE AGEN=INT(AGEN-1);
/*  IF  AGEN<=0 THEN AGEN=AGEN+100;*/
  AREA=INT(SITEID/10000000);
  IF  AREA = 1 THEN AREA2 = 1;
  ELSE IF 2 <= AREA <= 7 THEN AREA2 = 2;
  ELSE IF 8 <= AREA <=15 OR 19<= AREA <=20 THEN AREA2 = 3;
  ELSE IF 16<= AREA <=18 OR 21<= AREA <=24 THEN AREA2 = 4;
  ELSE IF 25<= AREA <=30 THEN AREA2 = 5;
  ELSE IF 31<= AREA <=39 THEN AREA2 = 6;
  ELSE IF 40<= AREA <=47 THEN AREA2 = 7;

  IF  0 <= AGEN < 5 THEN AGECAT1N=19;
  ELSE IF  5  <= AGEN < 10 THEN AGECAT1N=18;
  ELSE IF  10 <= AGEN < 15 THEN AGECAT1N=17;
  ELSE IF  15 <= AGEN < 20 THEN AGECAT1N=16;
  ELSE IF  20 <= AGEN < 25 THEN AGECAT1N=15;
  ELSE IF  25 <= AGEN < 30 THEN AGECAT1N=14;
  ELSE IF  30 <= AGEN < 35 THEN AGECAT1N=13;
  ELSE IF  35 <= AGEN < 40 THEN AGECAT1N=12;
  ELSE IF  40 <= AGEN < 45 THEN AGECAT1N=11;
  ELSE IF  45 <= AGEN < 50 THEN AGECAT1N=10;
  ELSE IF  50 <= AGEN < 55 THEN AGECAT1N=9;
  ELSE IF  55 <= AGEN < 60 THEN AGECAT1N=8;
  ELSE IF  60 <= AGEN < 65 THEN AGECAT1N=7;
  ELSE IF  65 <= AGEN < 70 THEN AGECAT1N=6;
  ELSE IF  70 <= AGEN < 75 THEN AGECAT1N=5;
  ELSE IF  75 <= AGEN < 80 THEN AGECAT1N=4;
  ELSE IF  80 <= AGEN < 85 THEN AGECAT1N=3;
  ELSE IF  85 <= AGEN < 90 THEN AGECAT1N=2;
  ELSE IF  90 <= AGEN      THEN AGECAT1N=1;
  ELSE  AGECAT1N=99;

  IF  0 <= AGEN < 16 THEN AGECAT2N=1;
  ELSE IF  16  <= AGEN < 30 THEN AGECAT2N=2;
  ELSE IF  30 <= AGEN < 65 THEN AGECAT2N=3;
  ELSE IF  65 <= AGEN      THEN AGECAT2N=4;
  ELSE  AGECAT2N=99;

  IF  AGECAT1N=99 OR AGECAT2N=99 THEN DELETE;

RUN ;

/*** MH ***/
PROC SORT DATA=WK01 ; BY MHDECOD; RUN ;
PROC SORT DATA=EXT ; BY MHDECOD; RUN ;

DATA  WK02;
  MERGE  WK01(IN=A DROP=MHTERM) EXT;
  BY  MHDECOD;
  IF A;
RUN ;

DATA  LIBADS.&FILE.;
  SET  WK02;
RUN;

%ADS_FIN;

/*** END ***/
